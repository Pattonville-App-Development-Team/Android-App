apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: "net.ltgt.errorprone"

def hasKeystore = file('psd.keystore.properties').exists()
def keystoreProperties = new Properties()
if (hasKeystore)
    keystoreProperties.load(file('psd.keystore.properties').newInputStream())
else
    println 'Keystore properties file missing! An unsigned release build will be generated.'

//Disable ErrorProne checks for generated code (Dagger 2, Room, etc.)
tasks.withType(JavaCompile) {
    options.compilerArgs += ['-XepDisableWarningsInGeneratedCode']
}

android {
    if (hasKeystore)
        signingConfigs {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    compileSdkVersion 27
    defaultConfig {
        applicationId "org.pattonvillecs.pattonvilleapp"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 18
        versionName "1.1.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        setProperty("archivesBaseName", "pattonvilleapp-v$versionName")
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-test.pro'
            if (hasKeystore)
                signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-test.pro'
            applicationIdSuffix ".debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    //Android Support Libraries
    implementation "com.android.support:support-annotations:$support_version"
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:support-v4:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.takisoft.fix:preference-v7:$support_version.0"

    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    //Firebase
    implementation "com.google.firebase:firebase-core:$firebase_version"
    implementation "com.google.firebase:firebase-messaging:$firebase_version"
    implementation "com.google.firebase:firebase-crash:$firebase_version"

    //Android Architecture Components
    implementation "android.arch.lifecycle:extensions:$arch_version"
    annotationProcessor "android.arch.lifecycle:compiler:$arch_version"
    testImplementation "android.arch.core:core-testing:$arch_version"
    implementation "android.arch.persistence.room:runtime:$arch_version"
    annotationProcessor "android.arch.persistence.room:compiler:$arch_version"
    implementation "android.arch.paging:runtime:1.0.0-alpha3"

    //Dagger 2
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

    //Calendar view
    implementation 'com.prolificinteractive:material-calendarview:1.4.3'

    //Home carousel
    implementation 'com.synnapps:carouselview:0.1.3'

    //Network image download + cache
    implementation 'com.squareup.picasso:picasso:2.5.2'

    //RecyclerView improvements
    implementation 'eu.davidea:flexible-adapter:5.0.0-rc1'

    //Java 8 streams
    implementation 'com.annimon:stream:1.1.8'

    //Collections, utilities
    implementation('com.google.guava:guava:23.0-android') {
        exclude group: 'com.google.code.findbugs'
    }

    //Network
    implementation 'com.android.volley:volley:1.0.0'

    //All for iCal4j library
    implementation 'org.mnode.ical4j:ical4j:2.0.5'
    implementation 'org.apache.commons:commons-collections4:4.1'
    implementation 'org.apache.commons:commons-lang3:3.6'
    implementation 'org.slf4j:slf4j-android:1.7.25'
    implementation 'org.slf4j:slf4j-api:1.7.25'

    //Tutorial
    implementation 'com.github.magneticflux-:Spotlight:1.3.2'

    //App settings
    implementation 'com.github.apl-devs:appintro:v4.2.2'

    //Serialization
    implementation 'com.esotericsoftware:kryo:4.0.1'
    implementation 'de.javakaffee:kryo-serializers:0.42'
    implementation 'org.objenesis:objenesis:2.6'

    //Pin button
    implementation 'com.github.varunest:sparkbutton:1.0.4'

    //News content parsing is in jsoup jar
    implementation 'org.jsoup:jsoup:1.10.3'

    //Fuzzy string contains - !!!GPL 3.0!!!
    implementation 'me.xdrop:fuzzywuzzy:1.1.8'

    //Alternative fuzzy string matching
    implementation 'org.apache.commons:commons-text:1.1'

    //Cool Particles
    implementation 'com.github.jinatonic.confetti:confetti:1.1.0'

    //Overrides Guava and Espresso's differing older versions
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-android:2.10.0'

    androidTestImplementation('com.android.support.test:runner:1.0.0') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.0') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation 'org.mockito:mockito-android:2.10.0'
}

apply plugin: 'com.google.gms.google-services'
repositories {
    mavenCentral()
} //Must be at bottom
