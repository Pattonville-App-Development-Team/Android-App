apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

def hasKeystore = file('../psd.keystore.properties').exists()
def keystoreProperties = new Properties()
if (hasKeystore)
    keystoreProperties.load(file('../psd.keystore.properties').newInputStream())
else
    println 'Keystore properties file missing! An unsigned release build will be generated.'


android {
    if (hasKeystore)
        signingConfigs {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "org.pattonvillecs.pattonvilleapp"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 18
        versionName "1.1.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        setProperty("archivesBaseName", "pattonvilleapp-v$versionName")
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (hasKeystore)
                signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled true
            //useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    // Run lint checks but don't abort build
    lintOptions {
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    compile "com.android.support:support-annotations:$support_version"
    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:design:$support_version"
    compile "com.android.support:support-v4:$support_version"
    compile "com.android.support:cardview-v7:$support_version"
    compile "com.android.support:recyclerview-v7:$support_version"
    compile "com.takisoft.fix:preference-v7:$support_version.0"

    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    compile "com.google.firebase:firebase-core:$firebase_version"
    compile "com.google.firebase:firebase-messaging:$firebase_version"
    compile "com.google.firebase:firebase-crash:$firebase_version"

    //Calendar view
    compile 'com.prolificinteractive:material-calendarview:1.4.3'

    //Home carousel
    compile 'com.synnapps:carouselview:0.1.3'

    //Network image download + cache
    compile 'com.squareup.picasso:picasso:2.5.2'

    //RecyclerView improvements
    compile 'eu.davidea:flexible-adapter:5.0.0-rc1'
    //Java 8 streams
    compile 'com.annimon:stream:1.1.8'

    //Collections, utilities
    compile('com.google.guava:guava:23.0-android') {
        exclude group: 'com.google.code.findbugs'
    }

    //Network
    compile 'com.android.volley:volley:1.0.0'

    //All for iCal4j library
    compile 'org.mnode.ical4j:ical4j:2.0.5'
    compile 'org.apache.commons:commons-collections4:4.1'
    compile 'org.apache.commons:commons-lang3:3.6'
    compile 'org.slf4j:slf4j-android:1.7.25'
    compile 'org.slf4j:slf4j-api:1.7.25'

    //Tutorial
    compile 'com.github.magneticflux-:Spotlight:1.3.2'

    //App settings
    compile 'com.github.apl-devs:appintro:v4.2.2'

    //Serialization
    compile 'com.esotericsoftware:kryo:4.0.1'
    compile 'de.javakaffee:kryo-serializers:0.42'

    //Pin button
    compile 'com.github.varunest:sparkbutton:1.0.4'

    //News content parsing is in jsoup jar
    compile 'org.jsoup:jsoup:1.10.3'

    //Fuzzy string contains - !!!GPL 3.0!!!
    compile 'me.xdrop:fuzzywuzzy:1.1.8'

    //Alternative fuzzy string matching
    compile 'org.apache.commons:commons-text:1.1'

    //Cool Particles
    compile 'com.github.jinatonic.confetti:confetti:1.1.0'

    //Overrides Guava and Espresso's differing older versions
    compile 'com.google.code.findbugs:jsr305:3.0.2'
    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test:runner:1.0.0') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:3.0.0') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}

apply plugin: 'com.google.gms.google-services' //Must be at bottom
